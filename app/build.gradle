apply plugin: 'com.android.application'

android {
    compileSdkVersion project.sdk
    defaultConfig {
        applicationId "club.syachiku.apkextractor"
        minSdkVersion project.minSdk
        targetSdkVersion project.sdk
        versionCode project.versionCode
        versionName project.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(KEY_STORE_FILE)
            storePassword KEY_STORE_PW
            keyAlias KEY_ALIAS
            keyPassword KEY_PW
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a": 1, "x86": 2]
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
            outputFileName = "apkextractor-${defaultConfig.versionName}-${variant.buildType.name}.apk"
//            variant.getPackageApplication().outputDirectory = new File("${project.rootDir.absolutePath.replace("android", "dist/v${defaultConfig.versionName}/android/${variant.buildType.name}")}")
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:' + project.supportVersion
    implementation 'com.android.support:cardview-v7:' + project.supportVersion
    implementation 'com.android.support:design:' + project.supportVersion
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.github.florent37:materialviewpager:1.2.3'
    implementation 'com.flaviofaria:kenburnsview:1.0.7'
    implementation 'com.jpardogo.materialtabstrip:library:1.1.0'
    implementation 'com.github.bumptech.glide:glide:4.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'